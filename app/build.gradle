apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'com.neenbedankt.android-apt'

Properties prop = new Properties()
prop.load(new FileInputStream(file("signing.properties")))

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"

    defaultConfig {
        applicationId 'com.robot.tuling'
        minSdkVersion 14
        targetSdkVersion 23
        versionCode 2
        versionName '1.2'
        manifestPlaceholders = [umengHolder: "Tuling"]
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    signingConfigs {
        release {
            storeFile file(prop['KEYSTORE_FILEPATH'])
            storePassword prop['STORE_PASSWORD']
            keyAlias prop['KEY_ALIAS']
            keyPassword prop['KEY_PASSWORD']
        }
    }

    buildTypes {
        release {
            //压缩资源
            shrinkResources true
            //zip优化
            zipAlignEnabled true
            //apk签名
            signingConfig signingConfigs.release
            //release版
            buildConfigField "boolean", "LOG_DEBUG", "true"

            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            applicationVariants.all {
                variant -> generateAppName(variant)
            }
        }

        debug{
            //压缩资源
            shrinkResources false
            //zip优化
            zipAlignEnabled false
            //apk签名
            signingConfig signingConfigs.release
            //debug版
            buildConfigField "boolean", "LOG_DEBUG", "false"

            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            applicationVariants.all {
                variant -> generateAppName(variant)
            }
        }
    }

    productFlavors {
        fir {manifestPlaceholders = [umengHolder: "fir"]}
        xiaomi {manifestPlaceholders = [umengHolder: "xiaomi"]}
        "360" {manifestPlaceholders = [umengHolder: "360"]}
        baidu {manifestPlaceholders = [umengHolder: "baidu"]}
    }

}

//生成自定义App名称
def generateAppName(variant) {
    variant.outputs.each {
        output -> output.outputFile = new File (output.outputFile.parent,
                "Tuling_${variant.buildType.name}_${variant.versionName}_${variant.productFlavors[0].name}.apk".toLowerCase())
    }
}

dependencies {

    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.jakewharton:butterknife:7.0.1'

    compile 'com.orhanobut:logger:1.4'

    compile 'com.android.support:appcompat-v7:23.2.1'
    compile 'com.android.support:design:23.2.1'
    compile 'com.android.support:recyclerview-v7:23.2.1'

    compile 'de.hdodenhof:circleimageview:2.0.0'
    compile 'com.lguipeng.bubbleview:library:1.0.0'

    compile('com.github.afollestad.material-dialogs:core:0.8.5.7@aar') {
        transitive = true
    }

    apt 'de.devland.esperandro:esperandro:2.1.0'
    compile 'de.devland.esperandro:esperandro-api:2.1.0'

    compile 'io.reactivex:rxandroid:1.1.0'

    compile 'com.squareup.retrofit2:retrofit:2.0.1'
    compile 'com.squareup.retrofit2:converter-gson:2.0.1'
    compile 'com.squareup.retrofit2:adapter-rxjava:2.0.1'
    compile 'com.google.code.gson:gson:2.6.2'

    compile 'com.umeng.analytics:analytics:latest.integration'

}
